# Maintainer: valr <valere dot monseur at ymail dot com>

_pkgname=dosfstools
pkgname=fsck-fat
pkgver=3.0.27
pkgrel=1
pkgdesc='fsck utility for fat filesystems'
url='https://github.com/valr/initsystem'
license=('GPL2')
groups=('initsystem')
arch=('x86_64')
makedepends=('musl-cross-compiler-x86_64' 'musl')
source=(https://github.com/$_pkgname/$_pkgname/releases/download/v$pkgver/$_pkgname-$pkgver.tar.xz{,.sig})
md5sums=('3254566ea66b185920abb7f694df2c2a'
         'SKIP')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make fsck.fat \
    CC=/opt/x86_64-linux-musl/bin/x86_64-linux-musl-gcc \
    CFLAGS='-D_GNU_SOURCE' \
    LDFLAGS='-static'
}

package () {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  install -Dm755 fsck.fat "${pkgdir}"/usr/bin/fsck.fat-static

  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
