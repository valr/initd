# Maintainer: valr <valere dot monseur at ymail dot com>

_pkgname=dosfstools

pkgname=fsck-fat
pkgver=3.0.26
pkgrel=1
pkgdesc='fsck utility for fat filesystems'
url='https://github.com/valr/initsystem'
license=('GPL2')
groups=('initsystem')
arch=('x86_64')
makedepends=('musl-cross-x86_64' 'musl-libc')
source=("http://www.daniel-baumann.ch/files/software/${_pkgname}/${_pkgname}-${pkgver}.tar.xz")
md5sums=('45012f5f56f2aae3afcd62120b9e5a08')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make fsck.fat \
    CC=/opt/cross/musl/x86_64-linux-musl/bin/x86_64-linux-musl-gcc \
    CFLAGS='-D_GNU_SOURCE' \
    LDFLAGS='-static'
}

package () {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  install -Dm755 fsck.fat "${pkgdir}"/usr/bin/fsck.fat-static

  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
