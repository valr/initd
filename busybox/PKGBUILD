# Maintainer: valr <valere dot monseur at ymail dot com>

pkgname=busybox
pkgver=1.24.2
pkgrel=2
pkgdesc='utilities for rescue and embedded systems'
arch=('i686' 'x86_64')
url='http://www.busybox.net/'
license=('GPL')
source=("$url/downloads/$pkgname-$pkgver.tar.bz2"
        'config_nosuid'
        'config_suid'
        http://busybox.net/downloads/fixes-"${pkgver}"/busybox-"${pkgver}"-{CVE-2016-2147,CVE-2016-2148,ash-recursive-heredocs}.patch)
md5sums=('2eaae519cac1143bcf583636a745381f'
         '7188c49bd2b3ab44d84061fb20c48f0e'
         'a1e5dedebe2f13b0c34ddfb0def8f9d5'
         'c45a85f5ced712743efbb683900f8c1d'
         '850a57ca2871e370b4916161a0320a3f'
         'b59eb7536609db1ab5215de860d9e558')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  for PATCH in "${srcdir}"/*.patch; do
    patch -p1 <${PATCH}
  done

  sed -i '1,1i#include <sys/resource.h>' include/libbb.h

  cp -r "${srcdir}/${pkgname}-${pkgver}" "${srcdir}"/nosuid
  cp "${srcdir}"/config_nosuid "${srcdir}"/nosuid/.config

  cp -r "${srcdir}/${pkgname}-${pkgver}" "${srcdir}"/suid
  cp "${srcdir}"/config_suid "${srcdir}"/suid/.config
}

build() {
  cd "${srcdir}"/nosuid
  make CFLAGS='-static -O -Wno-unused-result'

  cd "${srcdir}"/suid
  make CFLAGS='-static -O -Wno-unused-result'
}

package() {
  # setuid applets:
  # applets that have 'BB_SUID_REQUIRE' in include/applets.src.h
  # crontab, login, passwd, su, vlock, wall

  # binaries

  cd "${srcdir}"/nosuid
  install -D -m 755 busybox "${pkgdir}"/usr/bin/busybox

  cd "${srcdir}"/suid
  install -D -m 4755 busybox "${pkgdir}"/usr/bin/busybox-suid

  # symlinks: install applets in /usr/bin

  cd "${pkgdir}"/usr/bin
  ln -s /usr/bin/busybox ash

  # symlinks: install applets in /opt/busybox

  install -d "${pkgdir}"/opt/busybox
  cd "${pkgdir}"/opt/busybox

  for APPLET in $("${pkgdir}"/usr/bin/busybox --list); do
    case "${APPLET}" in
      halt|poweroff|reboot)
        ;;
      *)
        ln -s /usr/bin/busybox "${APPLET}"
        ;;
    esac
  done

  for APPLET in $("${pkgdir}"/usr/bin/busybox-suid --list); do
    ln -s /usr/bin/busybox-suid "${APPLET}"
  done

  install -D -m 644 "${srcdir}"/nosuid/README "${pkgdir}"/usr/share/doc/"${pkgname}"/README
  install -D -m 644 "${srcdir}"/nosuid/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
