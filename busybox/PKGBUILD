# Maintainer: Val√®re Monseur <valere dot monseur at ymail dot com>

pkgname=busybox
pkgver=1.22.1
pkgrel=1
pkgdesc='Utilities for rescue and embedded systems'
url='http://www.busybox.net/'
license=('GPL')
arch=('x86_64')
makedepends=('make' 'gcc' 'sed' 'ncurses' 'musl-cross-x86_64' 'musl-libc')
source=("http://www.busybox.net/downloads/${pkgname}-${pkgver}.tar.bz2"
        'config_nosuid'
        'config_suid')
md5sums=('337d1a15ab1cb1d4ed423168b1eb7d7e'
         'f0ec6f16030f84ec8f179c226cab9ccb'
         '818cd3efba9954cf33a9f8563e94e6d4')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '1,1i#include <sys/resource.h>' include/libbb.h

  # nosuid

  cp -r "${srcdir}/${pkgname}-${pkgver}" "${srcdir}"/nosuid
  cp "${srcdir}"/config_nosuid "${srcdir}"/nosuid/.config

  sed -i 's|LDLIBS += m crypt|LDLIBS += m|' "${srcdir}"/nosuid/Makefile.flags
  sed -i 's|# include <net/ethernet.h>||' "${srcdir}"/nosuid/networking/ifplugd.c

  # suid

  cp -r "${srcdir}/${pkgname}-${pkgver}" "${srcdir}"/suid
  cp "${srcdir}"/config_suid "${srcdir}"/suid/.config
}

build() {
  cd "${srcdir}"/nosuid
  make CC=/opt/cross/musl/x86_64-linux-musl/bin/x86_64-linux-musl-gcc \
    CFLAGS='-static -D_GNU_SOURCE -Wl,-z,muldefs'

  cd "${srcdir}"/suid
  make CC=/opt/cross/musl/x86_64-linux-musl/bin/x86_64-linux-musl-gcc \
    CFLAGS='-static -D_GNU_SOURCE -Wl,-z,muldefs'
}

package() {
  # setuid applets:
  # applets that have 'BB_SUID_REQUIRE' in include/applets.src.h
  # crontab, login, passwd, su ,vlock ,wall

  # binaries

  cd "${srcdir}"/nosuid
  install -Dm755 busybox "${pkgdir}"/usr/bin/busybox

  cd "${srcdir}"/suid
  install -Dm4755 busybox "${pkgdir}"/usr/bin/busybox-suid

  # symlinks: install ash in /usr/bin

  cd "${pkgdir}"/usr/bin
  ln -s /usr/bin/busybox ash

  # symlinks: install all applets in /opt/busybox

  install -d "${pkgdir}"/opt/busybox
  cd "${pkgdir}"/opt/busybox

  for APPLET in $("${pkgdir}"/usr/bin/busybox --list); do
    case "${APPLET}" in
      halt|poweroff|reboot)
        ;;
      *)
        ln -s /usr/bin/busybox "${APPLET}"
        ;;
    esac
  done

  for APPLET in $("${pkgdir}"/usr/bin/busybox-suid --list); do
    ln -s /usr/bin/busybox-suid "${APPLET}"
  done

  # license

  install -Dm644 "${srcdir}"/nosuid/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
