# Maintainer: valr <valere dot monseur at ymail dot com>

pkgname=busybox
pkgver=1.23.1
pkgrel=1
pkgdesc='utilities for rescue and embedded systems'
url='http://www.busybox.net/'
license=('GPL')
groups=('initsystem')
arch=('i686' 'x86_64')
source=("${url}/downloads/${pkgname}-${pkgver}.tar.bz2"
        'config_nosuid'
        'config_suid')
# http://busybox.net/downloads/fixes-${pkgver}/busybox-$pkgver-{ash,date,iplink,lzop,nc,zcat-no-ext}.patch
md5sums=('5c94d6301a964cd91619bd4d74605245'
         'fd275f9a6a77a7db6e7d48c6897d5b1b'
         'ab4d3af046ecf738a4813f5094b5f0a3')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

#  for PATCH in "${srcdir}"/*.patch; do
#    patch -p1 <${PATCH}
#  done

  sed -i '1,1i#include <sys/resource.h>' include/libbb.h

  # nosuid

  cp -r "${srcdir}/${pkgname}-${pkgver}" "${srcdir}"/nosuid
  cp "${srcdir}"/config_nosuid "${srcdir}"/nosuid/.config

  # suid

  cp -r "${srcdir}/${pkgname}-${pkgver}" "${srcdir}"/suid
  cp "${srcdir}"/config_suid "${srcdir}"/suid/.config
}

build() {
  cd "${srcdir}"/nosuid
  make CFLAGS='-static -O -Wno-unused-result'

  cd "${srcdir}"/suid
  make CFLAGS='-static -O -Wno-unused-result'
}

package() {
  # setuid applets:
  # applets that have 'BB_SUID_REQUIRE' in include/applets.src.h
  # crontab, login, passwd, su ,vlock ,wall

  # binaries

  cd "${srcdir}"/nosuid
  install -Dm755 busybox "${pkgdir}"/usr/bin/busybox

  cd "${srcdir}"/suid
  install -Dm4755 busybox "${pkgdir}"/usr/bin/busybox-suid

  # symlinks: install some applets in /usr/bin

  cd "${pkgdir}"/usr/bin
  ln -s /usr/bin/busybox ash

  # symlinks: install (almost) all applets in /opt/busybox

  install -d "${pkgdir}"/opt/busybox
  cd "${pkgdir}"/opt/busybox

  for APPLET in `"${pkgdir}"/usr/bin/busybox --list`; do
    case "${APPLET}" in
      halt|poweroff|reboot)
        ;;
      *)
        ln -s /usr/bin/busybox "${APPLET}"
        ;;
    esac
  done

  for APPLET in `"${pkgdir}"/usr/bin/busybox-suid --list`; do
    ln -s /usr/bin/busybox-suid "${APPLET}"
  done

  install -Dm644 "${srcdir}"/nosuid/README "${pkgdir}"/usr/share/doc/"${pkgname}"/README
  install -Dm644 "${srcdir}"/nosuid/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
