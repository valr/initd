# Maintainer: valr <valere dot monseur at ymail dot com>

pkgname=busybox
pkgver=1.23.1
pkgrel=1
pkgdesc='utilities for rescue and embedded systems'
url='http://www.busybox.net/'
license=('GPL')
groups=('initsystem')
arch=('x86_64')
makedepends=('musl-cross-compiler-x86_64' 'musl')
source=("${url}/downloads/${pkgname}-${pkgver}.tar.bz2"
        'config_nosuid'
        'config_suid')
# http://busybox.net/downloads/fixes-${pkgver}/busybox-$pkgver-{ash,date,iplink,lzop,nc,zcat-no-ext}.patch
md5sums=('5c94d6301a964cd91619bd4d74605245'
         'd04a67fb5c1d111488f162cc06e80b97'
         '30dabf72695a10aedb561e9eb47e7464')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

#  for PATCH in "${srcdir}"/*.patch; do
#    patch -p1 <${PATCH}
#  done

  sed -i '1,1i#include <sys/resource.h>' include/libbb.h

  # nosuid

  cp -r "${srcdir}/${pkgname}-${pkgver}" "${srcdir}"/nosuid
  cp "${srcdir}"/config_nosuid "${srcdir}"/nosuid/.config

  # patch to avoid redefinition of structure with musl
  sed -i 's|# include <net/ethernet.h>||' "${srcdir}"/nosuid/networking/ifplugd.c

  # with linux-api-headers 3.18, there is a redefinition of structure with musl
  # that can be solved by commenting out: #include <linux/in6.h> in the header
  # file /usr/include/linux/if_bridge.h

  # suid

  cp -r "${srcdir}/${pkgname}-${pkgver}" "${srcdir}"/suid
  cp "${srcdir}"/config_suid "${srcdir}"/suid/.config
}

build() {
  cd "${srcdir}"/nosuid
  make V=1 CC=/opt/x86_64-linux-musl/bin/x86_64-linux-musl-gcc \
    CFLAGS='-static -D_GNU_SOURCE -Wl,-z,muldefs'

  cd "${srcdir}"/suid
  make V=1 CC=/opt/x86_64-linux-musl/bin/x86_64-linux-musl-gcc \
    CFLAGS='-static -D_GNU_SOURCE -Wl,-z,muldefs'
}

package() {
  # setuid applets:
  # applets that have 'BB_SUID_REQUIRE' in include/applets.src.h
  # crontab, login, passwd, su ,vlock ,wall

  # binaries

  cd "${srcdir}"/nosuid
  install -Dm755 busybox "${pkgdir}"/usr/bin/busybox

  cd "${srcdir}"/suid
  install -Dm4755 busybox "${pkgdir}"/usr/bin/busybox-suid

  # symlinks: install some applets in /usr/bin

  cd "${pkgdir}"/usr/bin
  ln -s /usr/bin/busybox ash

  # symlinks: install (almost) all applets in /opt/busybox

  install -d "${pkgdir}"/opt/busybox
  cd "${pkgdir}"/opt/busybox

  for APPLET in `"${pkgdir}"/usr/bin/busybox --list`; do
    case "${APPLET}" in
      halt|poweroff|reboot)
        ;;
      *)
        ln -s /usr/bin/busybox "${APPLET}"
        ;;
    esac
  done

  for APPLET in `"${pkgdir}"/usr/bin/busybox-suid --list`; do
    ln -s /usr/bin/busybox-suid "${APPLET}"
  done

  install -Dm644 "${srcdir}"/nosuid/README "${pkgdir}"/usr/share/doc/"${pkgname}"/README
  install -Dm644 "${srcdir}"/nosuid/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
