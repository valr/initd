# Maintainer: valr <valere dot monseur at ymail dot com>

_pkgname=e2fsprogs
pkgname=fsck-ext
pkgver=1.42.12
pkgrel=1
pkgdesc='fsck utility for ext filesystems'
url='https://github.com/valr/initsystem'
license=('GPL' 'LGPL' 'MIT')
groups=('initsystem')
arch=('x86_64')
makedepends=('musl-cross-compiler-x86_64' 'musl')
source=("http://downloads.sourceforge.net/sourceforge/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        'MIT-LICENSE')
sha1sums=('083c1bb0d1e85672e8038a2fadf70b24e7409db7'
          'f4a0d5b0cdb980e3fedd6f5e7dde0b0ffb7bbdfb')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  CC=/opt/x86_64-linux-musl/bin/x86_64-linux-musl-gcc \
  CFLAGS='-D_GNU_SOURCE' \
  LDFLAGS='-static' \
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib \
    --disable-uuidd \
    --disable-backtrace \
    --disable-debugfs \
    --disable-imager \
    --disable-resizer \
    --disable-defrag \
    --enable-fsck \
    --disable-e2initrd-helper \
    --disable-tls \
    --disable-nls \
    --disable-rpath \
    --with-root-prefix=""

  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  install -Dm755 e2fsck/e2fsck "${pkgdir}"/usr/bin/fsck.ext-static

  install -Dm644 "${srcdir}"/MIT-LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/MIT-LICENSE
}
