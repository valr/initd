# Maintainer: valr <valere dot monseur at ymail dot com>

_pkgname=musl

pkgname=musl-libc
pkgver=1.1.4
pkgrel=1
pkgdesc='An implementation of the C/POSIX standard library'
url='http://www.musl-libc.org/'
license=('MIT')
arch=('x86_64')
depends=('musl-cross-x86_64')
options=('!strip' 'libtool' 'staticlibs' 'emptydirs')
source=("http://www.musl-libc.org/releases/${_pkgname}-${pkgver}.tar.gz")
md5sums=('f18f3bdbe088438cd64a5313c19a7312')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  CC=/opt/cross/musl/x86_64-linux-musl/bin/x86_64-linux-musl-gcc \
  CFLAGS=-fno-stack-protector \
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib/musl \
    --includedir=/usr/include/musl \
    --syslibdir=/usr/lib \
    --host=x86_64-linux-musl \
    --build=x86_64-linux-musl

  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install

  install -Dm644 COPYRIGHT "${pkgdir}"/usr/share/licenses/"${_pkgname}"/LICENSE
}
