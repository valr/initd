# Maintainer: valr <valere dot monseur at ymail dot com>

_udev_ver=220
pkgname=eudev
pkgver=3.2
pkgrel=1
pkgdesc='userspace device manager'
url='https://wiki.gentoo.org/wiki/Project:Eudev'
license=('GPL')
arch=('i686' 'x86_64')
backup=('etc/udev/udev.conf')
depends=('glib2' 'btrfs-progs' 'kmod' 'util-linux')
makedepends=('gobject-introspection' 'gperf')
provides=('systemd' 'libsystemd' "udev=${_udev_ver}" "libudev=${_udev_ver}" 'libudev.so')
conflicts=('systemd' 'libsystemd')
replaces=('systemd' 'libsystemd')
options=(!makeflags !libtool)
source=("https://dev.gentoo.org/~blueness/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "https://dev.gentoo.org/~blueness/${pkgname}/${pkgname}-${pkgver}.tar.gz.asc"
        'initcpio_hooks'
        'initcpio_install'
        'udev-hwdb.hook')
md5sums=('7e42d9b9ed8162021665139520676367'
         'SKIP'
         'e7bbe35b79cddd7c82bf201f6297722d'
         '46f3f81bbd7b9ab6638dc84e188eb06f'
         '7fce0d7c0ecace6c34e44fe6e24470ee')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libdir=/usr/lib \
    --enable-split-usr \
    --enable-introspection \
    --enable-manpages \
    --enable-kmod \
    --enable-hwdb \
    --with-rootprefix=/usr

    make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  for RULES in "${pkgdir}"/usr/lib/udev/rules.d/*.rules; do
    sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
               s#GROUP="tape"#GROUP="storage"#g;
               s#GROUP="cdrom"#GROUP="optical"#g' "${RULES}"
  done

  install -D -m 644 "${srcdir}"/initcpio_hooks   "${pkgdir}"/usr/lib/initcpio/hooks/udev
  install -D -m 644 "${srcdir}"/initcpio_install "${pkgdir}"/usr/lib/initcpio/install/udev
  install -D -m 644 "${srcdir}"/udev-hwdb.hook   "${pkgdir}"/usr/share/libalpm/hooks/udev-hwdb.hook

  install -D -m 644 COPYING "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
