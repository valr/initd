# Maintainer: valr <valere dot monseur at ymail dot com>

_udev_ver=220

pkgname=eudev
pkgver=3.1.2
pkgrel=1
pkgdesc='userspace device manager'
url='https://wiki.gentoo.org/wiki/Project:Eudev'
license=('GPL')
groups=('initsystem')
arch=('i686' 'x86_64')
backup=('etc/udev/udev.conf')
depends=('glib2' 'kbd' 'kmod' 'hwids' 'util-linux')
makedepends=('gobject-introspection' 'gperf' 'gtk-doc' 'intltool')
provides=("udev=${_udev_ver}"
          "libudev=${_udev_ver}"
          'libgudev-1.0.so'
          'libudev.so'
          'systemd' 'libsystemd') # fake because of archlinux packaging
conflicts=('systemd' 'libsystemd')
replaces=('systemd' 'libsystemd')
options=(!makeflags !libtool)
install='eudev.install'
source=("https://dev.gentoo.org/~blueness/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "https://dev.gentoo.org/~blueness/${pkgname}/${pkgname}-${pkgver}.tar.gz.asc"
        'initcpio_hooks'
        'initcpio_install')
md5sums=('07ab33fe310d250f06fc28f010e3fde2'
         'SKIP'
         'e7bbe35b79cddd7c82bf201f6297722d'
         'ac595576711e1e1b57ebe3f83493d2de')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libdir=/usr/lib \
    --enable-split-usr \
    --enable-introspection \
    --enable-gudev \
    --enable-kmod \
    --with-rootprefix=/usr \
    --with-html-dir=/usr/share/doc/"${pkgname}-${pkgver}"/html

    make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  for i in "${pkgdir}"/usr/lib/udev/rules.d/*.rules; do
    sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
               s#GROUP="tape"#GROUP="storage"#g;
               s#GROUP="cdrom"#GROUP="optical"#g' "${i}"
  done

  install -Dm644 "${srcdir}"/initcpio_hooks "${pkgdir}"/usr/lib/initcpio/hooks/udev
  install -Dm644 "${srcdir}"/initcpio_install "${pkgdir}"/usr/lib/initcpio/install/udev

  install -Dm644 README "${pkgdir}"/usr/share/doc/"${pkgname}"/README
  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
