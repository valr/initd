# Maintainer: valr <valere dot monseur at ymail dot com>

_udev_ver=217
pkgname=eudev
pkgver=2.1.1
pkgrel=3
pkgdesc='userspace device manager'
url='http://dev.gentoo.org/~blueness/eudev'
license=('GPL')
groups=('initsystem')
arch=('i686' 'x86_64')
backup=('etc/udev/udev.conf')
depends=('glib2' 'kbd' 'kmod' 'hwids' 'util-linux')
makedepends=('gobject-introspection' 'gperf' 'gtk-doc' 'intltool')
provides=("udev=${_udev_ver}"
          "libudev=${_udev_ver}"
          'libgudev-1.0.so'
          'libudev.so'
          'systemd' 'libsystemd') # fake because of archlinux packaging
conflicts=('systemd' 'libsystemd')
replaces=('systemd' 'libsystemd')
options=(!makeflags !libtool)
install='eudev.install'
source=("${url}/${pkgname}-${pkgver}.tar.gz"
        'initcpio_hooks'
        'initcpio_install')
md5sums=('dd945ce34314b4544e9036153d3a97e9'
         'e7bbe35b79cddd7c82bf201f6297722d'
         '1e5610279d24e77e1bf1547edd2f6d10')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libdir=/usr/lib \
    --enable-split-usr \
    --enable-introspection \
    --enable-gudev \
    --enable-rule-generator \
    --with-rootprefix=/usr \
    --with-html-dir=/usr/share/doc/"${pkgname}-${pkgver}"/html \
    --with-modprobe=/usr/bin/modprobe

    make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  for i in "${pkgdir}"/usr/lib/udev/rules.d/*.rules; do
    sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
               s#GROUP="tape"#GROUP="storage"#g;
               s#GROUP="cdrom"#GROUP="optical"#g' "${i}"
  done

  install -Dm644 "${srcdir}"/initcpio_hooks "${pkgdir}"/usr/lib/initcpio/hooks/udev
  install -Dm644 "${srcdir}"/initcpio_install "${pkgdir}"/usr/lib/initcpio/install/udev

  install -Dm644 README "${pkgdir}"/usr/share/doc/"${pkgname}"/README
  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
