# Maintainer: valr <valere dot monseur at ymail dot com>

pkgname=busybox
pkgver=1.32.0
pkgrel=1
pkgdesc='utilities for rescue and embedded systems'
url='http://www.busybox.net/'
license=('GPL')
arch=('x86_64' 'aarch64')
groups=('initsystem')
source=("${url}/downloads/${pkgname}-${pkgver}.tar.bz2"
        'config_nosuid'
        'config_suid'
        '0001-hwclock-make-glibc-2.31-compatible.patch')
        # http://busybox.net/downloads/fixes-"${pkgver}"/busybox-"${pkgver}"-{CVE-2016-2147,CVE-2016-2148,ash-recursive-heredocs}.patch
md5sums=('9576986f1a960da471d03b72a62f13c7'
         'b8e9f5719dc1bffad7aa18bc00d5c9c0'
         '4c5df64e701601ac1092065da7f005db'
         '67bc46326a0cb6ee86a3e661c7771a0f')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  for PATCH in "${srcdir}"/*.patch; do
    patch -p1 <${PATCH}
  done

  cp -r "${srcdir}/${pkgname}-${pkgver}" "${srcdir}"/nosuid
  cp "${srcdir}"/config_nosuid "${srcdir}"/nosuid/.config

  cp -r "${srcdir}/${pkgname}-${pkgver}" "${srcdir}"/suid
  cp "${srcdir}"/config_suid "${srcdir}"/suid/.config
}

build() {
  cd "${srcdir}"/nosuid
  make

  cd "${srcdir}"/suid
  make
}

package() {
  # setuid applets:
  # applets that have 'BB_SUID_REQUIRE' in include/applets.src.h
  # crontab, login, passwd, su, vlock, wall

  # binaries

  cd "${srcdir}"/nosuid
  install -D -m 755 busybox "${pkgdir}"/usr/bin/busybox

  cd "${srcdir}"/suid
  install -D -m 4755 busybox "${pkgdir}"/usr/bin/busybox-suid

  # symlinks: install applets in /usr/bin

  cd "${pkgdir}"/usr/bin
  ln -s /usr/bin/busybox ash

  # symlinks: install applets in /opt/busybox

  install -d -m 755 "${pkgdir}"/opt/busybox
  cd "${pkgdir}"/opt/busybox

  for APPLET in $("${pkgdir}"/usr/bin/busybox --list); do
    case "${APPLET}" in
      halt|poweroff|reboot)
        ;;
      *)
        ln -s /usr/bin/busybox "${APPLET}"
        ;;
    esac
  done

  for APPLET in $("${pkgdir}"/usr/bin/busybox-suid --list); do
    ln -s /usr/bin/busybox-suid "${APPLET}"
  done

  install -D -m 644 "${srcdir}"/nosuid/README "${pkgdir}"/usr/share/doc/"${pkgname}"/README
  install -D -m 644 "${srcdir}"/nosuid/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

# vim: set filetype=sh:
