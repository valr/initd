# Maintainer: valr <valere dot monseur at ymail dot com>

pkgname=initsystem
pkgver=1.2.0
pkgrel=1
pkgdesc='an init system'
url='https://github.com/valr/initsystem'
license=('GPL')
install='initsystem.install'
groups=('initsystem')
arch=('i686' 'x86_64')
backup=('etc/rc.conf' 'etc/rc.local' 'etc/rc.shutdown.local' 'etc/rc.respawn')
depends=('busybox' 'kbd' 'kmod' 'initramfs')
source=('init-og.c'
        'rc'
        'rc.conf'
        'rc.local'
        'rc.respawn'
        'rc.shutdown'
        'rc.shutdown.local'
        'rc.subr'
        'rc.d.udev'
        'shutdown'
        'LICENSE')
md5sums=('b3cf734052c09ec4b558a3454e31e571'
         '68721a7c03150fcb7121bd7f98bb5963'
         '8e3a9f55141c20031e8546be9c096cc7'
         '1deddd37535712bac2ab4bb4f7fa9a43'
         'e29a5c5846c5515cf659255d23094337'
         '60b9800cf83dc825d8b329bdc18c445b'
         '441535fedcec16c2c10767c92c6d3b4a'
         '9eb946cd5ab20397f4ca9131aa47416d'
         'f8de77c6c4541814eb3b5ba357154737'
         '8205440af7335a04a3e9a6d7c9a914f6'
         '4641e94ec96f98fabc56ff9cc48be14b')

build() {
  cd "${srcdir}"

  cc -Wall -static -o init-og init-og.c
}

package() {

  # configuration

  install -D -m 644 "${srcdir}"/rc.conf "${pkgdir}"/etc/rc.conf

  # binaries

  install -D -m 755 "${srcdir}"/init-og           "${pkgdir}"/usr/bin/init-og
  install -D -m 755 "${srcdir}"/rc                "${pkgdir}"/etc/rc
  install -D -m 755 "${srcdir}"/rc.local          "${pkgdir}"/etc/rc.local
  install -D -m 755 "${srcdir}"/rc.respawn        "${pkgdir}"/etc/rc.respawn
  install -D -m 755 "${srcdir}"/rc.shutdown       "${pkgdir}"/etc/rc.shutdown
  install -D -m 755 "${srcdir}"/rc.shutdown.local "${pkgdir}"/etc/rc.shutdown.local
  install -D -m 755 "${srcdir}"/rc.subr           "${pkgdir}"/etc/rc.subr
  install -D -m 755 "${srcdir}"/rc.d.udev         "${pkgdir}"/etc/rc.d/udev
  install -D -m 755 "${srcdir}"/shutdown          "${pkgdir}"/usr/bin/shutdown

  cd "${pkgdir}"/usr/bin

  ln -s shutdown halt
  ln -s shutdown poweroff
  ln -s shutdown reboot

  # license

  install -D -m 644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

# vim: set filetype=sh:
