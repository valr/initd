# Maintainer: valr <valere dot monseur at ymail dot com>

pkgname=initsystem
pkgver=1.2.0
pkgrel=1
pkgdesc='an init system'
url='https://github.com/valr/initsystem'
license=('GPL')
install='initsystem.install'
groups=('initsystem')
arch=('i686' 'x86_64')
backup=('etc/rc.conf' 'etc/rc.local' 'etc/rc.shutdown.local' 'etc/rc.respawn')
depends=('busybox' 'kbd' 'kmod' 'initramfs')
source=('init-og.c'
        'rc'
        'rc.conf'
        'rc.local'
        'rc.respawn'
        'rc.shutdown'
        'rc.shutdown.local'
        'rc.subr'
        'rc.d.udev'
        'shutdown'
        'LICENSE')
md5sums=('b3cf734052c09ec4b558a3454e31e571'
         'c849731f095e85407d6cea3b6983d493'
         'f76446b0256c581469f32e1ccf5deeff'
         '1091e654c9d3cb79f6abdd321274ecd2'
         'fe478d0901f955ee8dc155e46c14c1ff'
         '86a0dc3333f1e59693ff45a1758210a0'
         '112f6e0bd545836f9ca3eb7c9c6d7080'
         '547c42430d63f1a22596d3aa41569860'
         '032c86bc6bb00661631485bf74b6fd35'
         'b61a5a4850b11b2b79b0bee8b2b7eba8'
         '4641e94ec96f98fabc56ff9cc48be14b')

build() {
  cd "${srcdir}"

  cc -Wall -static -o init-og init-og.c
}

package() {

  # configuration

  install -D -m 644 "${srcdir}"/rc.conf "${pkgdir}"/etc/rc.conf

  # binaries

  install -D -m 755 "${srcdir}"/init-og           "${pkgdir}"/usr/bin/init-og
  install -D -m 755 "${srcdir}"/rc                "${pkgdir}"/etc/rc
  install -D -m 755 "${srcdir}"/rc.local          "${pkgdir}"/etc/rc.local
  install -D -m 755 "${srcdir}"/rc.respawn        "${pkgdir}"/etc/rc.respawn
  install -D -m 755 "${srcdir}"/rc.shutdown       "${pkgdir}"/etc/rc.shutdown
  install -D -m 755 "${srcdir}"/rc.shutdown.local "${pkgdir}"/etc/rc.shutdown.local
  install -D -m 755 "${srcdir}"/rc.subr           "${pkgdir}"/etc/rc.subr
  install -D -m 755 "${srcdir}"/rc.d.udev         "${pkgdir}"/etc/rc.d/udev
  install -D -m 755 "${srcdir}"/shutdown          "${pkgdir}"/usr/bin/shutdown

  cd "${pkgdir}"/usr/bin

  ln -s shutdown halt
  ln -s shutdown poweroff
  ln -s shutdown reboot

  # license

  install -D -m 644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

# vim: set filetype=sh:
