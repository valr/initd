# Maintainer: valr <valere dot monseur at ymail dot com>

pkgname=initsystem
pkgver=1.2.0
pkgrel=1
pkgdesc='an init system'
url='https://github.com/valr/initsystem'
license=('GPL')
install='initsystem.install'
groups=('initsystem')
arch=('x86_64' 'aarch64')
backup=('etc/rc.conf' 'etc/rc.local' 'etc/rc.shutdown.local' 'etc/rc.respawn')
depends=('busybox' 'dosfstools' 'e2fsprogs' 'kbd' 'kmod' 'mkinitcpio')
source=('initsystem.tar.gz'
        '99-initramfs.hook'
        '99-initramfs')
md5sums=('f7034ffb86641ab6c3460b9c99a1733e'
         '39683887ba00051a8aab753b62f3f73a'
         'fb944467dc23a750b051f09284a02dd1')

build() {
  make
}

package() {
  make bindir=/usr/bin sbindir=/usr/bin DESTDIR="${pkgdir}" install

  install -D -m 644 99-initramfs.hook "${pkgdir}"/usr/share/libalpm/hooks/99-initramfs.hook
  install -D -m 755 99-initramfs "${pkgdir}"/usr/share/libalpm/scripts/99-initramfs
  install -D -m 644 LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

# vim: set filetype=sh:
