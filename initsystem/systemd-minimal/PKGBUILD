# Maintainer: valr <valere dot monseur at ymail dot com>

_repo=http://mir.archlinux.fr/core/os/x86_64

pkgname='systemd-minimal'
pkgver=234.11
pkgrel=1
pkgdesc='minimal configuration from systemd'
url='https://www.github.com/systemd/systemd'
license=('GPL2')
groups=('initsystem')
arch=('any')
depends=('eudev' 'opentmpfiles')
provides=("systemd=${pkgver}" 'libsystemd')
conflicts=('systemd' 'libsystemd')
replaces=('systemd' 'libsystemd')
source=("${_repo}/systemd-${pkgver}-${pkgrel}-x86_64.pkg.tar.xz"
        "${_repo}/systemd-${pkgver}-${pkgrel}-x86_64.pkg.tar.xz.sig"
        'systemd-compare'
        'systemd-compare-pre.hook'
        'systemd-compare-post.hook')
md5sums=('ca14af26738ef7bcb48cd82417a63c15'
         'SKIP'
         '6baae720ebfaec3bd0f9a7f00d1f31ce'
         'a06c94ec7b1a2802ebb8d7f2b86ecd48'
         'd4cd8fbed18cf41de43c77d720d81367')

# The idea of this package is to keep the required non-binary files from 
# the systemd original package to avoid recreating and maintaining them by hand

package() {

  # directories to keep

  install -d "${pkgdir}"/etc/modules-load.d/
  install -d "${pkgdir}"/etc/sysctl.d/
  install -d "${pkgdir}"/etc/tmpfiles.d/

  install -d "${pkgdir}"/usr/lib/modules-load.d/
  install -d "${pkgdir}"/usr/lib/sysctl.d/
  install -d "${pkgdir}"/usr/lib/tmpfiles.d/

  # directories to follow-up for changes using systemd-compare

  install -d "${pkgdir}"/usr/lib/systemd/system
  install -d "${pkgdir}"/usr/lib/systemd/user
  install -d "${pkgdir}"/usr/lib/sysusers.d

  # files to keep

  mv "${srcdir}"/usr/lib/tmpfiles.d/etc.conf "${pkgdir}"/usr/lib/tmpfiles.d/etc.conf
  mv "${srcdir}"/usr/lib/tmpfiles.d/home.conf "${pkgdir}"/usr/lib/tmpfiles.d/home.conf
  mv "${srcdir}"/usr/lib/tmpfiles.d/legacy.conf "${pkgdir}"/usr/lib/tmpfiles.d/legacy.conf
  mv "${srcdir}"/usr/lib/tmpfiles.d/var.conf "${pkgdir}"/usr/lib/tmpfiles.d/var.conf
  mv "${srcdir}"/usr/lib/tmpfiles.d/x11.conf "${pkgdir}"/usr/lib/tmpfiles.d/x11.conf

  # systemd-compare tool

  install -D -m 755 "${srcdir}"/systemd-compare "${pkgdir}"/usr/share/libalpm/scripts/systemd-compare
  install -D -m 644 "${srcdir}"/systemd-compare-pre.hook "${pkgdir}"/usr/share/libalpm/hooks/aaa-systemd-compare-pre.hook
  install -D -m 644 "${srcdir}"/systemd-compare-post.hook "${pkgdir}"/usr/share/libalpm/hooks/zzz-systemd-compare-post.hook
}
