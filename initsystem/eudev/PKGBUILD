# Maintainer: valr <valere dot monseur at ymail dot com>

_pkgver=234.11

pkgname=eudev
pkgver=3.2.2
pkgrel=1
pkgdesc='device manager in userspace'
url='https://wiki.gentoo.org/wiki/Project:Eudev'
license=('GPL2')
groups=('initsystem')
arch=('i686' 'x86_64')
backup=('etc/udev/udev.conf')
depends=('glib2' 'kmod' 'util-linux')
makedepends=('gobject-introspection' 'gperf')
optdepends=('btrfs-progs: btrfs utilities for eudev rules')
provides=("udev=${_pkgver}" 'libudev.so')
conflicts=('udev' 'libudev.so')
replaces=('udev' 'libudev.so')
source=("https://dev.gentoo.org/~blueness/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "https://dev.gentoo.org/~blueness/${pkgname}/${pkgname}-${pkgver}.tar.gz.asc"
        'rc.d.eudev'
        'initcpio_hooks'
        'initcpio_install'
        'udev-hwdb.hook')
md5sums=('41e19b70462692fefd072a3f38818b6e'
         'SKIP'
         'd29b35a1d38a7f7686e016d8a1daec54'
         'fccfb30c927640b2cf50f97c6566562a'
         '99c642e36a6da3ef1591a119eb0b4e7a'
         '7fce0d7c0ecace6c34e44fe6e24470ee')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libdir=/usr/lib \
    --enable-split-usr \
    --enable-introspection \
    --enable-hwdb \
    --with-rootprefix=/usr

    make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  for RULES in "${pkgdir}"/usr/lib/udev/rules.d/*.rules; do
    sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
               s#GROUP="tape"#GROUP="storage"#g;
               s#GROUP="cdrom"#GROUP="optical"#g' "${RULES}"
  done

  install -D -m 755 "${srcdir}"/rc.d.eudev       "${pkgdir}"/etc/rc.d/eudev
  install -D -m 644 "${srcdir}"/initcpio_hooks   "${pkgdir}"/usr/lib/initcpio/hooks/udev
  install -D -m 644 "${srcdir}"/initcpio_install "${pkgdir}"/usr/lib/initcpio/install/udev
  install -D -m 644 "${srcdir}"/udev-hwdb.hook   "${pkgdir}"/usr/share/libalpm/hooks/udev-hwdb.hook

  install -D -m 644 COPYING "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

# vim: set filetype=sh:
