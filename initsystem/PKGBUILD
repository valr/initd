# Maintainer: valr <valere dot monseur at ymail dot com>

pkgname=initsystem
pkgver=1.0.0
pkgrel=1
pkgdesc='An init system'
url='https://github.com/valr/initsystem'
license=('GPL')
groups=('initsystem')
arch=('x86_64')
backup=('etc/rc.conf' 'etc/rc.local' 'etc/rc.shutdown.local')
depends=('musl-cross-x86_64' 'musl-libc' 'busybox' 'eudev' 'kbd')
source=('init.c'
        'rc'
        'rc.conf'
        'rc.local'
        'rc.respawn'
        'rc.shutdown'
        'rc.shutdown.local'
        'shutdown'
        'tmpfiles'
        'rc.subr'
        'rc.d.acpi'
        'rc.d.alsa'
        'rc.d.bumblebee'
        'rc.d.cron'
        'rc.d.cups'
        'rc.d.dbus'
        'rc.d.iptables'
        'rc.d.ip6tables'
        'rc.d.syslog'
        'rc.d.tlp'
        'rc.d.udev'
        'LICENSE')
md5sums=('8063252b587fc69fa70d1a8f467abbac'
         'c0f0ebaa3dd2d4da3fc8387da40fc094'
         'cd6cec15484be20a115fef515ede7b36'
         'ff50ab2abfbc5896c135b98ea9aedffa'
         '2e9ebe16c96033b2f6e942608af008e1'
         '58e9c8c2c3c6a081c5ebbb3195c1c1ce'
         'bc1bc45109cc83378b9650fdd8035d05'
         '71f1f64dc86f79cf7d74c988a85de66f'
         'faf41c841242d8d499fbaba52397d2dd'
         '844bfb30356bb2a96ec021411bc0c5a0'
         'f27521286bcd6c9f29570438033ba5c1'
         '64a6e42a1e472f5d33bb6f6a60792d0e'
         '858c013e5f771795371e174b9edc1821'
         '0ab6ab1b2b74959166d5796bfe95e9ce'
         '733e8431ffa1b2251c99d6fb199e7d9e'
         '4194a49432c8d250cc3e461e68f17206'
         'd565c6651abeffd4ad7d6b820608e5c6'
         '0d7abaf0fba4c697529771b7d3f54816'
         'dc49a9cfb2d7cb90b83518dbea36e298'
         '056fba5e3ebf0acd63d3f4696f15e7ca'
         'f934c9bf8fc8f41f8438e55dfc10681c'
         '4641e94ec96f98fabc56ff9cc48be14b')

build() {
  cd "${srcdir}"

  /opt/cross/musl/x86_64-linux-musl/bin/x86_64-linux-musl-gcc -Wall -static -o init init.c
}

package() {

  # configuration

  install -Dm644 "${srcdir}"/rc.conf "${pkgdir}"/etc/rc.conf

  # binaries

  install -Dm755 "${srcdir}"/init              "${pkgdir}"/usr/bin/init
  install -Dm755 "${srcdir}"/rc                "${pkgdir}"/etc/rc
  install -Dm755 "${srcdir}"/rc.local          "${pkgdir}"/etc/rc.local
  install -Dm755 "${srcdir}"/rc.respawn        "${pkgdir}"/etc/rc.respawn
  install -Dm755 "${srcdir}"/rc.shutdown       "${pkgdir}"/etc/rc.shutdown
  install -Dm755 "${srcdir}"/rc.shutdown.local "${pkgdir}"/etc/rc.shutdown.local
  install -Dm755 "${srcdir}"/shutdown          "${pkgdir}"/usr/bin/shutdown
  install -Dm755 "${srcdir}"/tmpfiles          "${pkgdir}"/usr/bin/tmpfiles

  # services

  install -Dm755 "${srcdir}"/rc.subr        "${pkgdir}"/etc/rc.subr
  install -Dm755 "${srcdir}"/rc.d.acpi      "${pkgdir}"/etc/rc.d/acpi
  install -Dm755 "${srcdir}"/rc.d.alsa      "${pkgdir}"/etc/rc.d/alsa
  install -Dm755 "${srcdir}"/rc.d.bumblebee "${pkgdir}"/etc/rc.d/bumblebee
  install -Dm755 "${srcdir}"/rc.d.cron      "${pkgdir}"/etc/rc.d/cron
  install -Dm755 "${srcdir}"/rc.d.cups      "${pkgdir}"/etc/rc.d/cups
  install -Dm755 "${srcdir}"/rc.d.dbus      "${pkgdir}"/etc/rc.d/dbus
  install -Dm755 "${srcdir}"/rc.d.iptables  "${pkgdir}"/etc/rc.d/iptables
  install -Dm755 "${srcdir}"/rc.d.ip6tables "${pkgdir}"/etc/rc.d/ip6tables
  install -Dm755 "${srcdir}"/rc.d.tlp       "${pkgdir}"/etc/rc.d/tlp
  install -Dm755 "${srcdir}"/rc.d.syslog    "${pkgdir}"/etc/rc.d/syslog
  install -Dm755 "${srcdir}"/rc.d.udev      "${pkgdir}"/etc/rc.d/udev

  # symlinks

  cd "${pkgdir}"/usr/bin

  ln -s shutdown halt
  ln -s shutdown poweroff
  ln -s shutdown reboot

  # license

  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
