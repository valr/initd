# Maintainer: valr <valere dot monseur at ymail dot com>

pkgname=initsystem
pkgver=1.0.0
pkgrel=1
pkgdesc='an init system'
url='https://github.com/valr/initsystem'
license=('GPL')
groups=('initsystem')
arch=('x86_64')
depends=('busybox' 'kbd')
makedepends=('musl-cross-compiler-x86_64' 'musl')
optdepends=('eudev: replaces udev from systemd')
backup=('etc/rc.conf' 'etc/rc.local' 'etc/rc.shutdown.local' 'etc/rc.respawn')
install='initsystem.install'
source=('init.c'
        'rc'
        'rc.conf'
        'rc.local'
        'rc.respawn'
        'rc.shutdown'
        'rc.shutdown.local'
        'rc.subr'
        'shutdown'
        'tmpfiles'
        'rc.subr'
        'LICENSE')
md5sums=('e9a774c66616bec3ef97c56bd7f58160'
         '54be417577fd46edb4fcfeee206c6edc'
         '1b4271c6c291150199994abd3e3daf04'
         'f9d345202573c58dfea5f7d39530b36d'
         'a6781031e16aab769c200c7f69d3735c'
         'bdfcee0074b852198166681b665dd435'
         '8c53ae0e2e5584999f47f905bc226e5f'
         '43821e4310cedaa91aaff9667bf0383a'
         '07c56d75dd64e0a04a3cccf28f5d7e97'
         '9d0cce73f5c7e7dd3f1cc8bf33de9f88'
         '43821e4310cedaa91aaff9667bf0383a'
         '4641e94ec96f98fabc56ff9cc48be14b')

build() {
  cd "${srcdir}"

  /opt/x86_64-linux-musl/bin/x86_64-linux-musl-gcc -Wall -static -o init init.c
}

package() {

  # configuration

  install -Dm644 "${srcdir}"/rc.conf "${pkgdir}"/etc/rc.conf

  # binaries

  install -Dm755 "${srcdir}"/init              "${pkgdir}"/usr/bin/init
  install -Dm755 "${srcdir}"/rc                "${pkgdir}"/etc/rc
  install -Dm755 "${srcdir}"/rc.local          "${pkgdir}"/etc/rc.local
  install -Dm755 "${srcdir}"/rc.respawn        "${pkgdir}"/etc/rc.respawn
  install -Dm755 "${srcdir}"/rc.shutdown       "${pkgdir}"/etc/rc.shutdown
  install -Dm755 "${srcdir}"/rc.shutdown.local "${pkgdir}"/etc/rc.shutdown.local
  install -Dm755 "${srcdir}"/rc.subr           "${pkgdir}"/etc/rc.subr
  install -Dm755 "${srcdir}"/shutdown          "${pkgdir}"/usr/bin/shutdown
  install -Dm755 "${srcdir}"/tmpfiles          "${pkgdir}"/usr/bin/tmpfiles

  # services

  install -dm755 "${pkgdir}"/etc/rc.d

  # symlinks

  cd "${pkgdir}"/usr/bin

  ln -s shutdown halt
  ln -s shutdown poweroff
  ln -s shutdown reboot

  # license

  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
