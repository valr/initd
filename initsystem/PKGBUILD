# Maintainer: valr <valere dot monseur at ymail dot com>

pkgname=initsystem
pkgver=1.1.0
pkgrel=1
pkgdesc='an init system'
url='https://github.com/valr/initsystem'
license=('GPL')
arch=('i686' 'x86_64')
depends=('busybox' 'kbd')
optdepends=('eudev: replaces udev from systemd')
backup=('etc/rc.conf' 'etc/rc.local' 'etc/rc.shutdown.local' 'etc/rc.respawn')
install='initsystem.install'
source=('init-og.c'
        'rc.conf'
        'rc'
        'rc.local'
        'rc.respawn'
        'rc.shutdown'
        'rc.shutdown.local'
        'rc.subr'
        'shutdown'
        'tmpfiles'
        'LICENSE')
md5sums=('82b4e82d46e72e4bbfc02e4b670745ea'
         'c2204cbe9137bfe5842ae6a515847394'
         '2dea1fa1e77db0e0a545e84ec85512ee'
         'd21512ddfd60b3f15f54f04a120a8e86'
         '10e17beee700ee7ec9cebe188bf6e241'
         'c7760407a2bfb707a92675637bbcbe2f'
         '9779378412f0772b64ee5c01eaf99684'
         '9764f1cccb6a891bdedafba7f3e8841f'
         '4d156389c1e51bcfa12088cc7c14a7bd'
         '62d9997ecb8f626c7749b44e786f7eba'
         '4641e94ec96f98fabc56ff9cc48be14b')

build() {
  cd "${srcdir}"

  cc -Wall -static -o init-og init-og.c
}

package() {

  # configuration

  install -D -m 644 "${srcdir}"/rc.conf "${pkgdir}"/etc/rc.conf

  # binaries

  install -D -m 755 "${srcdir}"/init-og           "${pkgdir}"/usr/bin/init-og
  install -D -m 755 "${srcdir}"/rc                "${pkgdir}"/etc/rc
  install -D -m 755 "${srcdir}"/rc.local          "${pkgdir}"/etc/rc.local
  install -D -m 755 "${srcdir}"/rc.respawn        "${pkgdir}"/etc/rc.respawn
  install -D -m 755 "${srcdir}"/rc.shutdown       "${pkgdir}"/etc/rc.shutdown
  install -D -m 755 "${srcdir}"/rc.shutdown.local "${pkgdir}"/etc/rc.shutdown.local
  install -D -m 755 "${srcdir}"/rc.subr           "${pkgdir}"/etc/rc.subr
  install -D -m 755 "${srcdir}"/shutdown          "${pkgdir}"/usr/bin/shutdown
  install -D -m 755 "${srcdir}"/tmpfiles          "${pkgdir}"/usr/bin/tmpfiles

  # services

  install -d -m 755 "${pkgdir}"/etc/rc.d

  # symlinks

  cd "${pkgdir}"/usr/bin

  ln -s shutdown halt
  ln -s shutdown poweroff
  ln -s shutdown reboot

  # license

  install -D -m 644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
