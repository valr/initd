# Maintainer: valr <valere dot monseur at ymail dot com>

pkgname=initsystem
pkgver=1.0.0
pkgrel=1
pkgdesc='an init system'
url='https://github.com/valr/initsystem'
license=('GPL')
groups=('initsystem')
arch=('x86_64')
depends=('busybox' 'kbd')
makedepends=('musl-cross-compiler-x86_64' 'musl')
optdepends=('eudev: replaces udev from systemd')
backup=('etc/rc.conf' 'etc/rc.local' 'etc/rc.shutdown.local')
source=('init.c'
        'rc'
        'rc.conf'
        'rc.local'
        'rc.respawn'
        'rc.shutdown'
        'rc.shutdown.local'
        'shutdown'
        'tmpfiles'
        'rc.subr'
        'rc.d.acpi'
        'rc.d.alsa'
        'rc.d.bumblebee'
        'rc.d.cron'
        'rc.d.cups'
        'rc.d.dbus'
        'rc.d.eudev'
        'rc.d.iptables'
        'rc.d.ip6tables'
        'rc.d.syslog'
        'rc.d.tlp'
        'rc.d.udev'
        'LICENSE')
md5sums=('8063252b587fc69fa70d1a8f467abbac'
         '9feba9e68fdfd9402fa35872fa93ccec'
         '3e50cf7eb99f778d09f95d81b0314999'
         'e5b9861aa212352be75ee06d456bddf1'
         '255d60c70913c2eb1c9811cbe363190d'
         'dd2a13865e2ace073887411f0ae33056'
         '27168efadfa0262b0feed0ffd9adbf02'
         '8e8b1d9e6f23e59dcc5bfba39f4f34a5'
         'fb41eb11251ae765b3873786d18f9b1b'
         'ebb2b2bb22c08fa6a0411b6fec1d9eae'
         'e00510599f62458bb1e8b8610924c9e9'
         '6fd3ad2827b1e7ac09bfe8bfc0d6fa40'
         '11f0416ff45208439fb638a433c90a10'
         '1cf903928218fe507f9a93f7208145fb'
         'fba02ce5db7274182bbef5ce16aa79f2'
         '46f5fa79ea5ade6416af9ebd3ee1b08e'
         'c2f4e490b32daf4f1bd182761abf08a9'
         '60e9633394864060f8d6c67732815634'
         '520e3a2bcc746152dde6204fdbfa7d38'
         '6782c4665a48d581e47ec2cb46ac0f28'
         '952a122df44ad62d7a31c2f50363c0d0'
         '633b4d0e9868e13daa822daaaf7e6250'
         '4641e94ec96f98fabc56ff9cc48be14b')

build() {
  cd "${srcdir}"

  /opt/x86_64-linux-musl/bin/x86_64-linux-musl-gcc -Wall -static -o init init.c
}

package() {

  # configuration

  install -Dm644 "${srcdir}"/rc.conf "${pkgdir}"/etc/rc.conf

  # binaries

  install -Dm755 "${srcdir}"/init              "${pkgdir}"/usr/bin/init
  install -Dm755 "${srcdir}"/rc                "${pkgdir}"/etc/rc
  install -Dm755 "${srcdir}"/rc.local          "${pkgdir}"/etc/rc.local
  install -Dm755 "${srcdir}"/rc.respawn        "${pkgdir}"/etc/rc.respawn
  install -Dm755 "${srcdir}"/rc.shutdown       "${pkgdir}"/etc/rc.shutdown
  install -Dm755 "${srcdir}"/rc.shutdown.local "${pkgdir}"/etc/rc.shutdown.local
  install -Dm755 "${srcdir}"/shutdown          "${pkgdir}"/usr/bin/shutdown
  install -Dm755 "${srcdir}"/tmpfiles          "${pkgdir}"/usr/bin/tmpfiles

  # services

  install -Dm755 "${srcdir}"/rc.subr        "${pkgdir}"/etc/rc.subr
  install -Dm755 "${srcdir}"/rc.d.acpi      "${pkgdir}"/etc/rc.d/acpi
  install -Dm755 "${srcdir}"/rc.d.alsa      "${pkgdir}"/etc/rc.d/alsa
  install -Dm755 "${srcdir}"/rc.d.bumblebee "${pkgdir}"/etc/rc.d/bumblebee
  install -Dm755 "${srcdir}"/rc.d.cron      "${pkgdir}"/etc/rc.d/cron
  install -Dm755 "${srcdir}"/rc.d.cups      "${pkgdir}"/etc/rc.d/cups
  install -Dm755 "${srcdir}"/rc.d.dbus      "${pkgdir}"/etc/rc.d/dbus
  install -Dm755 "${srcdir}"/rc.d.eudev     "${pkgdir}"/etc/rc.d/eudev
  install -Dm755 "${srcdir}"/rc.d.iptables  "${pkgdir}"/etc/rc.d/iptables
  install -Dm755 "${srcdir}"/rc.d.ip6tables "${pkgdir}"/etc/rc.d/ip6tables
  install -Dm755 "${srcdir}"/rc.d.tlp       "${pkgdir}"/etc/rc.d/tlp
  install -Dm755 "${srcdir}"/rc.d.syslog    "${pkgdir}"/etc/rc.d/syslog
  install -Dm755 "${srcdir}"/rc.d.udev      "${pkgdir}"/etc/rc.d/udev

  # symlinks

  cd "${pkgdir}"/usr/bin

  ln -s shutdown halt
  ln -s shutdown poweroff
  ln -s shutdown reboot

  # license

  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
