# Maintainer: valr <valere dot monseur at ymail dot com>

pkgname=initsystem
pkgver=1.0.0
pkgrel=1
pkgdesc='an init system'
url='https://github.com/valr/initsystem'
license=('GPL')
groups=('initsystem')
arch=('i686' 'x86_64')
depends=('busybox' 'kbd')
optdepends=('eudev: replaces udev from systemd')
backup=('etc/rc.conf' 'etc/rc.local' 'etc/rc.shutdown.local' 'etc/rc.respawn')
install='initsystem.install'
source=('init.c'
        'rc.conf'
        'rc'
        'rc.local'
        'rc.respawn'
        'rc.shutdown'
        'rc.shutdown.local'
        'rc.subr'
        'shutdown'
        'tmpfiles'
        'LICENSE')
md5sums=('82b4e82d46e72e4bbfc02e4b670745ea'
         '887ac1eedd45b0f6715375e15c32f469'
         'a2a4f6896a26643f01c980bc898625fb'
         'f9d345202573c58dfea5f7d39530b36d'
         'a6781031e16aab769c200c7f69d3735c'
         'bdf026eee198ed00b780caabbb511c8f'
         '8c53ae0e2e5584999f47f905bc226e5f'
         '70b45924d33ebbd5cc2f32cb37c0d9cc'
         '07c56d75dd64e0a04a3cccf28f5d7e97'
         'ab33b386c1a5b7c4415300f69d4a6dfa'
         '4641e94ec96f98fabc56ff9cc48be14b')

build() {
  cd "${srcdir}"

  cc -Wall -static -o init init.c
}

package() {

  # configuration

  install -Dm644 "${srcdir}"/rc.conf "${pkgdir}"/etc/rc.conf

  # binaries

  install -Dm755 "${srcdir}"/init              "${pkgdir}"/usr/bin/init
  install -Dm755 "${srcdir}"/rc                "${pkgdir}"/etc/rc
  install -Dm755 "${srcdir}"/rc.local          "${pkgdir}"/etc/rc.local
  install -Dm755 "${srcdir}"/rc.respawn        "${pkgdir}"/etc/rc.respawn
  install -Dm755 "${srcdir}"/rc.shutdown       "${pkgdir}"/etc/rc.shutdown
  install -Dm755 "${srcdir}"/rc.shutdown.local "${pkgdir}"/etc/rc.shutdown.local
  install -Dm755 "${srcdir}"/rc.subr           "${pkgdir}"/etc/rc.subr
  install -Dm755 "${srcdir}"/shutdown          "${pkgdir}"/usr/bin/shutdown
  install -Dm755 "${srcdir}"/tmpfiles          "${pkgdir}"/usr/bin/tmpfiles

  # services

  install -dm755 "${pkgdir}"/etc/rc.d

  # symlinks

  cd "${pkgdir}"/usr/bin

  ln -s shutdown halt
  ln -s shutdown poweroff
  ln -s shutdown reboot

  # license

  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
