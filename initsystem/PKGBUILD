# Maintainer: valr <valere dot monseur at ymail dot com>

pkgname=initsystem
pkgver=1.1.0
pkgrel=1
pkgdesc='an init system'
url='https://github.com/valr/initsystem'
license=('GPL')
arch=('i686' 'x86_64')
depends=('busybox' 'kbd')
optdepends=('eudev: replaces udev from systemd')
backup=('etc/rc.conf' 'etc/rc.local' 'etc/rc.shutdown.local' 'etc/rc.respawn')
install='initsystem.install'
source=('init-og.c'
        'rc.conf'
        'rc'
        'rc.local'
        'rc.respawn'
        'rc.shutdown'
        'rc.shutdown.local'
        'rc.subr'
        'shutdown'
        'tmpfiles'
        'LICENSE')
md5sums=('59205da903f0ed1d32ed32fcfed498cf'
         'c2204cbe9137bfe5842ae6a515847394'
         'b4411a9148847f2f7b8083604bceeb53'
         'd21512ddfd60b3f15f54f04a120a8e86'
         '10e17beee700ee7ec9cebe188bf6e241'
         'f70868b51c8fcc45c88edb57d6d34a99'
         '9779378412f0772b64ee5c01eaf99684'
         '9764f1cccb6a891bdedafba7f3e8841f'
         '4d156389c1e51bcfa12088cc7c14a7bd'
         '1967c02f5b3053fa8d59e9489c3572a7'
         '4641e94ec96f98fabc56ff9cc48be14b')

build() {
  cd "${srcdir}"

  cc -Wall -static -o init-og init-og.c
}

package() {

  # directories

  install -d -m 755 "${pkgdir}"/etc/rc.d
  install -d -m 755 "${pkgdir}"/etc/tmpfiles.d
  install -d -m 755 "${pkgdir}"/usr/lib/tmpfiles.d

  # configuration

  install -D -m 644 "${srcdir}"/rc.conf "${pkgdir}"/etc/rc.conf

  # binaries

  install -D -m 755 "${srcdir}"/init-og           "${pkgdir}"/usr/bin/init-og
  install -D -m 755 "${srcdir}"/rc                "${pkgdir}"/etc/rc
  install -D -m 755 "${srcdir}"/rc.local          "${pkgdir}"/etc/rc.local
  install -D -m 755 "${srcdir}"/rc.respawn        "${pkgdir}"/etc/rc.respawn
  install -D -m 755 "${srcdir}"/rc.shutdown       "${pkgdir}"/etc/rc.shutdown
  install -D -m 755 "${srcdir}"/rc.shutdown.local "${pkgdir}"/etc/rc.shutdown.local
  install -D -m 755 "${srcdir}"/rc.subr           "${pkgdir}"/etc/rc.subr
  install -D -m 755 "${srcdir}"/shutdown          "${pkgdir}"/usr/bin/shutdown
  install -D -m 755 "${srcdir}"/tmpfiles          "${pkgdir}"/usr/bin/tmpfiles

  # symlinks

  cd "${pkgdir}"/usr/bin

  ln -s shutdown halt
  ln -s shutdown poweroff
  ln -s shutdown reboot

  # license

  install -D -m 644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
