# Maintainer: valr <valere dot monseur at ymail dot com>

pkgname=initsystem
pkgver=1.2.0
pkgrel=1
pkgdesc='an init system'
url='https://github.com/valr/initsystem'
license=('GPL')
arch=('i686' 'x86_64')
depends=('busybox' 'kbd')
optdepends=('eudev: replaces udev from systemd')
backup=('etc/rc.conf' 'etc/rc.local' 'etc/rc.shutdown.local' 'etc/rc.respawn')
install='initsystem.install'
source=('init-og.c'
        'rc'
        'rc.conf'
        'rc.local'
        'rc.respawn'
        'rc.shutdown'
        'rc.shutdown.local'
        'rc.subr'
        'shutdown'
        'tmpfiles'
        'LICENSE')
md5sums=('b3cf734052c09ec4b558a3454e31e571'
         '1395a89c666b14b4be400f217489a44c'
         'd3f6b27bcf720ffab46f45e3a4e6e21b'
         '1091e654c9d3cb79f6abdd321274ecd2'
         'c9f3d7ad6e3f5686f8aca221999d5f3a'
         '3333640322376e9a99b0dda774b91e35'
         '112f6e0bd545836f9ca3eb7c9c6d7080'
         'f183825c8babdbe8b8ab04fd993f7116'
         '2213426c9f53f7f0469e4a8c6eca2e90'
         '38e43225381636f2c37ff82260c89374'
         '4641e94ec96f98fabc56ff9cc48be14b')

build() {
  cd "$srcdir"

  cc -Wall -static -o init-og init-og.c
}

package() {
  # directories

  install -d -m 755 "$pkgdir"/etc/rc.d
  install -d -m 755 "$pkgdir"/etc/tmpfiles.d
  install -d -m 755 "$pkgdir"/usr/lib/tmpfiles.d

  # configuration

  install -D -m 644 "$srcdir"/rc.conf "$pkgdir"/etc/rc.conf

  # binaries

  install -D -m 755 "$srcdir"/init-og           "$pkgdir"/usr/bin/init-og
  install -D -m 755 "$srcdir"/rc                "$pkgdir"/etc/rc
  install -D -m 755 "$srcdir"/rc.local          "$pkgdir"/etc/rc.local
  install -D -m 755 "$srcdir"/rc.respawn        "$pkgdir"/etc/rc.respawn
  install -D -m 755 "$srcdir"/rc.shutdown       "$pkgdir"/etc/rc.shutdown
  install -D -m 755 "$srcdir"/rc.shutdown.local "$pkgdir"/etc/rc.shutdown.local
  install -D -m 755 "$srcdir"/rc.subr           "$pkgdir"/etc/rc.subr
  install -D -m 755 "$srcdir"/shutdown          "$pkgdir"/usr/bin/shutdown
  install -D -m 755 "$srcdir"/tmpfiles          "$pkgdir"/usr/bin/tmpfiles

  # symlinks

  cd "$pkgdir"/usr/bin

  ln -s shutdown halt
  ln -s shutdown poweroff
  ln -s shutdown reboot

  # license

  install -D -m 644 "$srcdir"/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim: set filetype=sh:
