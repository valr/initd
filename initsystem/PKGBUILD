# Maintainer: valr <valere dot monseur at ymail dot com>

pkgname=initsystem
pkgver=1.0.0
pkgrel=1
pkgdesc='an init system'
url='https://github.com/valr/initsystem'
license=('GPL')
groups=('initsystem')
arch=('x86_64')
depends=('busybox' 'kbd')
makedepends=('musl-cross-compiler-x86_64' 'musl')
optdepends=('eudev: replaces udev from systemd')
backup=('etc/rc.conf' 'etc/rc.local' 'etc/rc.shutdown.local')
install='initsystem.install'
source=('init.c'
        'rc'
        'rc.conf'
        'rc.local'
        'rc.respawn'
        'rc.shutdown'
        'rc.shutdown.local'
        'shutdown'
        'tmpfiles'
        'rc.subr'
        'LICENSE')
md5sums=('d20ccb0b26510b77f0ebf9c48f6fab0f'
         '54be417577fd46edb4fcfeee206c6edc'
         'dc9a14dcf7637913e7d4390a9139a2b2'
         'f9d345202573c58dfea5f7d39530b36d'
         '0016d7bd9a0a85316796387789c03ba4'
         'e1df3be4eed6263e3d046cd5f52106a2'
         '8c53ae0e2e5584999f47f905bc226e5f'
         '07c56d75dd64e0a04a3cccf28f5d7e97'
         '9d0cce73f5c7e7dd3f1cc8bf33de9f88'
         'ebb2b2bb22c08fa6a0411b6fec1d9eae'
         '4641e94ec96f98fabc56ff9cc48be14b')

build() {
  cd "${srcdir}"

  /opt/x86_64-linux-musl/bin/x86_64-linux-musl-gcc -Wall -static -o init init.c
}

package() {

  # configuration

  install -Dm644 "${srcdir}"/rc.conf "${pkgdir}"/etc/rc.conf

  # binaries

  install -Dm755 "${srcdir}"/init              "${pkgdir}"/usr/bin/init
  install -Dm755 "${srcdir}"/rc                "${pkgdir}"/etc/rc
  install -Dm755 "${srcdir}"/rc.local          "${pkgdir}"/etc/rc.local
  install -Dm755 "${srcdir}"/rc.respawn        "${pkgdir}"/etc/rc.respawn
  install -Dm755 "${srcdir}"/rc.shutdown       "${pkgdir}"/etc/rc.shutdown
  install -Dm755 "${srcdir}"/rc.shutdown.local "${pkgdir}"/etc/rc.shutdown.local
  install -Dm755 "${srcdir}"/shutdown          "${pkgdir}"/usr/bin/shutdown
  install -Dm755 "${srcdir}"/tmpfiles          "${pkgdir}"/usr/bin/tmpfiles

  # services

  install -dm755 "${pkgdir}"/etc/rc.d

  # symlinks

  cd "${pkgdir}"/usr/bin

  ln -s shutdown halt
  ln -s shutdown poweroff
  ln -s shutdown reboot

  # license

  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
