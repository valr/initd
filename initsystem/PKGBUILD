# Maintainer: valr <valere dot monseur at ymail dot com>

pkgname=initsystem
pkgver=1.0.0
pkgrel=50
pkgdesc='An init system'
url='https://github.com/valr/initsystem'
license=('GPL')
groups=('initsystem')
arch=('x86_64')
depends=('busybox' 'musl-cross-x86_64' 'musl-libc' 'systemd' 'kbd')
source=('init.c'
        'rc'
        'rc.respawn'
        'rc.shutdown'
        'shutdown'
        'tmpfiles'
        'rc.subr'
        'rc.d.udev'
        'LICENSE')
md5sums=('2c22b7b55d84caee9c70f272e9a456eb'
         'e5f933d3dc421471d6261b3d2a9cb468'
         '0622ba6ad1aab20c3b495468f5e9ab72'
         'a6a97228538712087a624578d4eb5825'
         '3a7f605f6087d90d65cb48f11e819325'
         'faf41c841242d8d499fbaba52397d2dd'
         '03e7875121e97dae7927293cb6b63027'
         '347c281129c020797252a92c3eb5047e'
         '4641e94ec96f98fabc56ff9cc48be14b')

build() {
  cd "${srcdir}"

  /opt/cross/musl/x86_64-linux-musl/bin/x86_64-linux-musl-gcc -Wall -static -o init init.c
}

package() {

  # binaries

  install -Dm755 "${srcdir}"/init        "${pkgdir}"/sbin/init
  install -Dm755 "${srcdir}"/rc          "${pkgdir}"/etc/rc
  install -Dm755 "${srcdir}"/rc.respawn  "${pkgdir}"/etc/rc.respawn
  install -Dm755 "${srcdir}"/rc.shutdown "${pkgdir}"/etc/rc.shutdown
  install -Dm755 "${srcdir}"/shutdown    "${pkgdir}"/usr/bin/shutdown
  install -Dm755 "${srcdir}"/tmpfiles    "${pkgdir}"/usr/bin/tmpfiles

  # services

  install -Dm755 "${srcdir}"/rc.subr   "${pkgdir}"/etc/rc.subr
  install -Dm755 "${srcdir}"/rc.d.udev "${pkgdir}"/etc/rc.d/udev

  # symlinks

  cd "${pkgdir}"/usr/bin

  ln -s shutdown halt
  ln -s shutdown poweroff
  ln -s shutdown reboot

  # license

  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
