# Maintainer: valr <valere dot monseur at ymail dot com>

pkgname=initsystem
pkgver=1.0.0
pkgrel=50
pkgdesc='An init system'
url='https://github.com/valr/initsystem'
license=('GPL')
groups=('initsystem')
arch=('x86_64')
depends=('busybox' 'musl-cross-x86_64' 'musl-libc' 'systemd' 'kbd')
source=('init.c'
        'rc'
        'rc.respawn'
        'rc.shutdown'
        'shutdown'
        'tmpfiles'
        'rc.subr'
        'rc.d.syslog'
        'rc.d.udev'
        'LICENSE')
md5sums=('8063252b587fc69fa70d1a8f467abbac'
         '9665707da7558aabad921e5135abaa57'
         '2e9ebe16c96033b2f6e942608af008e1'
         '73ddf4051369e2c73178b15f75cb5ecf'
         '3a7f605f6087d90d65cb48f11e819325'
         'faf41c841242d8d499fbaba52397d2dd'
         '5c9b8dd6065219452ec080837c60d861'
         '7fc4bcbcac0931d431d4f013da4263fd'
         'c3740ec2921b6b77fa07a717fbb3a0fe'
         '4641e94ec96f98fabc56ff9cc48be14b')

build() {
  cd "${srcdir}"

  /opt/cross/musl/x86_64-linux-musl/bin/x86_64-linux-musl-gcc -Wall -static -o init init.c
}

package() {

  # binaries

  install -Dm755 "${srcdir}"/init        "${pkgdir}"/usr/bin/init
  install -Dm755 "${srcdir}"/rc          "${pkgdir}"/etc/rc
  install -Dm755 "${srcdir}"/rc.respawn  "${pkgdir}"/etc/rc.respawn
  install -Dm755 "${srcdir}"/rc.shutdown "${pkgdir}"/etc/rc.shutdown
  install -Dm755 "${srcdir}"/shutdown    "${pkgdir}"/usr/bin/shutdown
  install -Dm755 "${srcdir}"/tmpfiles    "${pkgdir}"/usr/bin/tmpfiles

  # services

  install -Dm755 "${srcdir}"/rc.subr     "${pkgdir}"/etc/rc.subr
  install -Dm755 "${srcdir}"/rc.d.syslog "${pkgdir}"/etc/rc.d/syslog
  install -Dm755 "${srcdir}"/rc.d.udev   "${pkgdir}"/etc/rc.d/udev

  # symlinks

  cd "${pkgdir}"/usr/bin

  ln -s shutdown halt
  ln -s shutdown poweroff
  ln -s shutdown reboot

  # license

  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
