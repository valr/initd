# Maintainer: valere dot monseur at ymail dot com

pkgname=perp
pkgver=2.07
pkgrel=1
pkgdesc='A persistent process supervisor'
arch=('i686' 'x86_64')
url='http://b0llix.net/perp/'
license=('GPL')
makedepends=('musl')
source=("http://b0llix.net/perp/distfiles/${pkgname}-${pkgver}.tar.gz")
md5sums=('a2acc7425d556d9635a25addcee9edb5')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i "s|CC = gcc|CC = musl-gcc|" conf.mk
  sed -i "s|-DNDEBUG -O2|-DNDEBUG -O2 -static|" conf.mk
  sed -i "s|/usr/sbin|/usr/bin|" conf.mk
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # binaries

  make DESTDIR="${pkgdir}" install

  # cleaning

  rm "${pkgdir}"/usr/bin/perp-setup
  rm "${pkgdir}"/usr/bin/tinylog_run
  rm "${pkgdir}"/usr/share/man/man8/perp-setup.8

  # license

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
