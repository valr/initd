# Maintainer: Val√®re Monseur <valere dot monseur at ymail dot com>

pkgname=musl
pkgver=1.0.0
pkgrel=1
pkgdesc='An implementation of the C/POSIX standard library'
arch=('i686' 'x86_64')
url='http://www.musl-libc.org/'
license=('MIT')
options=(staticlibs)
source=("http://www.musl-libc.org/releases/${pkgname}-${pkgver}.tar.gz")
md5sums=('e54664fdf211d27737e328c4462b545e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --libdir=/usr/lib/musl --includedir=/usr/include/musl --syslibdir=/usr/lib
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # binaries

  make DESTDIR="${pkgdir}/" install

  # license

  install -Dm644 COPYRIGHT "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
