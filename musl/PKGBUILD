# Maintainer: valr <valere dot monseur at ymail dot com>

pkgname=musl
pkgver=1.1.5
pkgrel=1
pkgdesc='an implementation of the C/POSIX standard library'
url='http://www.musl-libc.org/'
license=('MIT')
groups=('initsystem')
arch=('x86_64')
depends=('musl-cross-compiler-x86_64')
options=('staticlibs' '!buildflags')
source=("http://www.musl-libc.org/releases/${pkgname}-${pkgver}.tar.gz")
md5sums=('94f8aa9dab80229fed68991bb9984cc5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  CC=/opt/x86_64-linux-musl/bin/x86_64-linux-musl-gcc \
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib/musl \
    --includedir=/usr/include/musl \
    --syslibdir=/usr/lib \
    --host=x86_64-linux-musl \
    --build=x86_64-linux-musl

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 README "${pkgdir}"/usr/share/doc/"${pkgname}"/README
  install -Dm644 COPYRIGHT "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYRIGHT
}
