# Maintainer: Val√®re Monseur <valere dot monseur at ymail dot com>

_pkgname=e2fsprogs
pkgname=fsck
pkgver=1.42.10
pkgrel=1
pkgdesc='fsck filesystem utility for ext3/4'
url='https://github.com/valr/initd'
license=('GPL' 'LGPL' 'MIT')
groups=('initd')
arch=('x86_64')
makedepends=('bc' 'musl-cross-x86_64' 'musl-libc')
source=("http://downloads.sourceforge.net/sourceforge/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        'MIT-LICENSE')
sha1sums=('06eba8a78ce1d5032fdcaf34f09025f01ceaca79'
          'f4a0d5b0cdb980e3fedd6f5e7dde0b0ffb7bbdfb')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  sed -i 's|#include <linux/fd.h>|/*#include <linux/fd.h>*/|' misc/filefrag.c
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  CC=/opt/cross/musl/x86_64-linux-musl/bin/x86_64-linux-musl-gcc \
  CFLAGS='-D_GNU_SOURCE -D__uint64_t=u_int64_t' LDFLAGS='-static' \
  ./configure --prefix=/usr --with-root-prefix="" --libdir=/usr/lib \
              --enable-fsck \
              --disable-backtrace --disable-debugfs --disable-imager \
              --disable-resizer --disable-defrag --disable-e2initrd-helper \
              --disable-tls --disable-uuidd --disable-nls --disable-rpath

  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  # binaries

  install -Dm755 e2fsck/e2fsck "${pkgdir}"/usr/bin/fsck.ext3-static
  install -Dm755 e2fsck/e2fsck "${pkgdir}"/usr/bin/fsck.ext4-static

  # license

  install -Dm644 "${srcdir}"/MIT-LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/MIT-LICENSE
}
