# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=colord
pkgname=(colord colord-sane)
pkgver=1.4.3
pkgrel=1
pkgdesc="System daemon for managing color devices"
url="https://www.freedesktop.org/software/colord"
arch=(x86_64)
license=(GPL2)
depends=(lcms2 libgusb polkit sqlite systemd dconf dbus libgudev shared-mime-info)
makedepends=(gobject-introspection vala sane bash-completion argyllcms git docbook-utils
             docbook-sgml perl-sgmls meson gtk-doc)
options=(!emptydirs)
_commit=cd7faac0d81f2dabef6c9787baf78b9e8fb0e1ae  # tags/1.4.3^0
source=("git+https://github.com/hughsie/colord#commit=$_commit")
sha256sums=('SKIP')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17')  # Richard Hughes

pkgver() {
  cd colord
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd colord
}

build() {
  arch-meson colord build \
    -D libcolordcompat=true \
    -D sane=true \
    -D vapi=true \
    -D print_profiles=true \
    -D daemon_user=colord \
    -D systemd=false
  ninja -C build
}

check() {
  cd build
  meson test
}

package_colord() {
  optdepends=('argyllcms: color profiling'
              'colord-sane: SANE support')
  replaces=(shared-color-profiles)

  DESTDIR="$pkgdir" ninja -C build install

  echo 'u colord - "Color management daemon" /var/lib/colord' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/colord.conf"

### Split colord-sane
  mkdir -p colord-sane/usr/lib/colord-plugins
  mv {"$pkgdir",colord-sane}/usr/lib/colord-sane
  mv {"$pkgdir",colord-sane}/usr/lib/colord-plugins/libcolord_sensor_sane.so
}

package_colord-sane() {
  pkgdesc+=" (SANE support)"
  depends=("colord=$pkgver-$pkgrel" sane)
  mv colord-sane/* "$pkgdir"
}

# vim:set sw=2 et:
